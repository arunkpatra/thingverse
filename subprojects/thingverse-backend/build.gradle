plugins {
    id 'java-library'
    id 'jacoco'
    id 'scala'
    id 'org.springframework.boot' version '2.4.2'
    id 'com.bmuschko.docker-remote-api' version '6.4.0'
    id 'com.gorylenko.gradle-git-properties' version '2.2.2'
}

import com.bmuschko.gradle.docker.tasks.DockerVersion
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

java {
    withJavadocJar()
    withSourcesJar()
}
jar {
    enabled = true
}
bootJar {
    archiveClassifier.set('boot')
}
test {
    useJUnitPlatform()
}
gitProperties {
    failOnNoGitDirectory = false
}
springBoot  {
    buildInfo()
}
dependencies {
    api enforcedPlatform(project(':thingverse-bom'))

    implementation project(':thingverse-common')
    implementation project(':thingverse-security')
    implementation project(':thingverse-consul-autoconfig')
    implementation project(':thingverse-kubernetes')
    implementation project(':thingverse-logging')
    implementation project(':thingverse-grpc-client')
    implementation project(':thingverse-grpc-common')
    implementation project(':thingverse-backend-api')
    implementation project(':thingverse-tracing')

    implementation "io.grpc:grpc-netty-shaded"

    implementation(project(':thingverse-cassandra-backend'))
    implementation "com.typesafe.akka:akka-cluster-sharding-typed_2.12"
    implementation "com.typesafe.akka:akka-persistence-typed_2.12"
    implementation "com.typesafe.akka:akka-persistence-query_2.12"
    implementation "com.typesafe.akka:akka-persistence_2.12"
    implementation "com.typesafe.akka:akka-serialization-jackson_2.12"
    implementation "com.typesafe.akka:akka-cluster-tools_2.12"
    implementation "com.typesafe.akka:akka-http_2.12"
    implementation "com.typesafe.akka:akka-http2-support_2.12"
    implementation "com.typesafe.akka:akka-http-jackson_2.12"

    implementation("com.typesafe.akka:akka-persistence-cassandra_2.12") {
        exclude group: 'com.typesafe.akka', module: 'akka-persistence_2.12'
        exclude group: 'com.typesafe.akka', module: 'akka-persistence-query_2.12'
        exclude group: 'com.typesafe.akka', module: 'akka-cluster-tools_2.12'
    }

    implementation "com.typesafe.akka:akka-persistence-cassandra-launcher_2.12"
    implementation "com.datastax.oss:java-driver-core:4.5.0"
    implementation "com.lightbend.akka:akka-stream-alpakka-cassandra_2.12"
    implementation "com.ecwid.consul:consul-api"
    implementation "io.dropwizard.metrics:metrics-core"
    implementation "io.netty:netty-handler"
    implementation "org.codehaus.janino:janino"
    implementation 'org.springframework.boot:spring-boot'
    implementation 'org.springframework.boot:spring-boot-autoconfigure'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor:2.4.1'
    implementation 'org.springframework.cloud:spring-cloud-commons'
    implementation "com.typesafe.akka:akka-actor_2.12"
    implementation "com.typesafe.akka:akka-http-core_2.12"
    implementation "com.typesafe.akka:akka-http-spray-json_2.12"
    implementation "com.typesafe.akka:akka-discovery_2.12"
    implementation "com.typesafe.akka:akka-stream_2.12"
    implementation "com.typesafe.akka:akka-actor-typed_2.12"
    implementation "com.typesafe.akka:akka-cluster-sharding-typed_2.12"
    implementation "com.typesafe.akka:akka-persistence-typed_2.12"
    implementation "com.typesafe.akka:akka-persistence-query_2.12"
    implementation "com.typesafe.akka:akka-serialization-jackson_2.12"
    implementation "com.typesafe.akka:akka-cluster-tools_2.12"
    implementation "com.typesafe.akka:akka-cluster-metrics_2.12"
    implementation "com.typesafe.akka:akka-http_2.12"
    implementation "com.typesafe.akka:akka-http-jackson_2.12"
    implementation("com.lightbend.akka.grpc:akka-grpc-runtime_2.12") {
        exclude group: 'com.typesafe.akka', module: 'akka-stream_2.12'
        exclude group: 'com.typesafe.akka', module: 'akka-discovery_2.12'
    }
    implementation("com.lightbend.akka.management:akka-management-cluster-bootstrap_2.12") {
        exclude group: 'com.typesafe.akka', module: 'akka-cluster_2.12'
        exclude group: 'com.typesafe.akka', module: 'akka-discovery_2.12'
        exclude group: 'com.typesafe.akka', module: 'akka-http-core_2.12'
        exclude group: 'com.typesafe.akka', module: 'akka-http-spray-json_2.12'
    }
    implementation("com.lightbend.akka.discovery:akka-discovery-consul_2.12") {
        exclude group: 'com.typesafe.akka', module: 'akka-actor_2.12'
        exclude group: 'com.typesafe.akka', module: 'akka-discovery_2.12'
        exclude group: 'com.typesafe.akka', module: 'akka-http_2.12'
        exclude group: 'com.typesafe.akka', module: 'akka-http-spray-json_2.12'
        exclude group: 'com.typesafe.akka', module: 'akka-stream_2.12'
    }
    implementation("com.lightbend.akka.discovery:akka-discovery-kubernetes-api_2.12") {
        exclude group: 'com.typesafe.akka', module: 'akka-actor_2.12'
        exclude group: 'com.typesafe.akka', module: 'akka-discovery_2.12'
    }
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-autoconfigure'
    implementation 'org.springframework.boot:spring-boot-actuator-autoconfigure'
    implementation 'de.codecentric:spring-boot-admin-starter-client'
    implementation 'org.jolokia:jolokia-core'

    testImplementation "com.typesafe.akka:akka-actor-testkit-typed_2.12"
    testImplementation "junit:junit"
    testImplementation "commons-io:commons-io"
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: 'com.vaadin.external.google', module: 'android-json'
    }
}

publishing {
    publications {
        thingverseBackend(MavenPublication) {
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            from components.java
            artifact bootJar
            pom {
                name = 'Thingverse Backend'
                description = 'Thingverse Akka Backend'
            }
        }
    }
}

String getDockerRegistryUrl()  {
    String url = System.getenv('DOCKER_REGISTRY')
    return null != url ? url : project.properties.getOrDefault('docker.registry', "")
}

task checkIfDockerRegistrySpecified(type: DefaultTask) {
    doFirst {
        if (getDockerRegistryUrl().length() != 0) {
            project.ext["docker.registry"] = getDockerRegistryUrl()
        } else {
            println "WARNING:Could not locate docker registry. You can provide either the DOCKER_REGISTRY " +
                    "environment variable or the docker.registry system property, e.g -Ddocker.registry=localhost:5000. " +
                    "The gradle.properties file of the root project may also be used."
        }
    }
}

task checkIfDockerIsRunning(type: DockerVersion) {
    dependsOn checkIfDockerRegistrySpecified
    onError { exception ->
        project.ext['docker.not.running'] = true
        println "WARNING: Docker is not running. Can't perform any Docker related tasks."
    }
}

task buildDockerImage(type: DockerBuildImage) {
    dependsOn checkIfDockerIsRunning, bootJar
    onlyIf { !project.hasProperty('docker.not.running') && project.hasProperty('docker.registry')}
    inputDir.set(file('.'))
    images.addAll("${project.name}:${project.version}",
            "${getDockerRegistryUrl()}/${project.name}:${project.version}")
    onError {  e -> println("Can't build image. Error is: ${e.message}")}
}

task pushDockerImage(type: DockerPushImage) {
    dependsOn buildDockerImage
    onlyIf { !project.hasProperty('docker.not.running') && project.hasProperty('docker.registry') }
    images.addAll("${getDockerRegistryUrl()}/${project.name}:${project.version}")
    onError { exception ->
        println("Can't push image. Error is: " + exception.message)
    }
}

docker {
    registryCredentials {
        url.set("http://${getDockerRegistryUrl()}/v2/")
    }
}